{{!
@param IRI exercise
}}

{{> sparql/prefixes.mustache }}

SELECT (REPLACE(STR(?prerequisite), "^.*\\/([^/]+)$", "$1") AS ?id)
       ?name
WHERE {
  ?prerequisite a sp:Query .
  FILTER (!sameTerm(<{{exercise}}>, ?prerequisite))
  FILTER NOT EXISTS {
    ?prerequisite dcterms:subject ?languageConstruct .
    FILTER NOT EXISTS {
      <{{exercise}}> dcterms:subject ?languageConstruct .
    }
  }
  # Prerequisite must have the same or easier difficulty than the selected exercise.
  <{{exercise}}> sparqlab:hasDifficulty ?difficulty .
  sparqlab:difficultyLevels skos:memberList/rdf:rest* [
    rdf:first ?prerequisiteDifficulty ;
    rdf:rest*/rdf:first ?difficulty
  ] .
  ?prerequisite rdfs:label ?name ;
    sparqlab:hasDifficulty ?prerequisiteDifficulty .
}
